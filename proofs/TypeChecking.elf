of : tm -> tp -> type.

of/empty : of empty unit.
of/obj : of L Tl -> of R Tr -> of (obj L R) (prod Tl Tr).
of/getLeft : of (getLeft O) A
          <- of O (prod A B).
of/getRight : of (getRight O) B
	    <- of O (prod A B).
of/left : of (left L) (sum Lt Rt M)
       <- of L Lt
       <- tp-mode-safe Rt Rm
       <- tp-mode-safe Lt Lm
       <- ub Lm Rm M.
of/right : of (right R)  (sum Lt Rt M)
       <- of R Rt
       <- tp-mode-safe Rt Rm
       <- tp-mode-safe Lt Lm
       <- ub Lm Rm M.
% switch should be GLB not LUB, but right now just making them equal seems good enough.
of/switch : of (switch L R O) C
	      <- of O (sum A B M)
	      <- of R (B ~> C) 
	      <- of L (A ~> C)
	      <- tp-mode B M
	      <- tp-mode A M.

of/logic-var : tp-mode-safe B neg -> of logic-var B.

of/subsump : of E T'
          <- of E T
          <- T =< T'
          <- tp-mode-safe T' M. % derivations can be specialized.

of/@s : of Ef (A ~> C) -> of Ev A -> of (@s Ef Ev) C.

of/lam : of (lam F A) (A ~> B)
      <- tp-mode-safe B OutMode
      <- tp-mode-safe A InMode
      <- ({x}{a} safe-moded a -> a =< A -> of x a -> of (F x) B).

%block assm-b : some {x : tm}{t} block {_:of x t}.
%block all-b = ( assm-b | sub-b | tp-mode-always-safe | tm-b).
%worlds (all-b) (of _ _).


used-as-neg : {Result : mode } of E T -> of E' T' -> type.

used-as-neg/e : used-as-neg neg A A.

used-as-neg/lam-inner : ({x}{a}{sm_a}{s}{of_x} used-as-neg ResM OfA (F_Of x a sm_a s of_x)) 
		      -> used-as-neg ResM OfA (of/lam F_Of _ _).

used-as-neg/app-l : used-as-neg ResM OfX OfE1
                 -> used-as-neg ResM OfX (of/@s OfE1 OfE2).

used-as-neg/app-r : used-as-neg ResM OfX (of/@s OfF  (OfV : of _ Vt))
		 <- tp-mode Vt neg
                 <- used-as-neg neg OfX OfV.

used-as-neg/objL : used-as-neg ResM OfX (of/obj (OfL : of _ Lt) OfR)
		<- tp-mode Lt neg
                <- used-as-neg ResM OfX OfL.

used-as-neg/objR : used-as-neg ResM OfX (of/obj OfL (OfR : of _ Rt))
		<- tp-mode Rt neg
                <- used-as-neg ResM OfX OfR.

used-as-neg/product : used-as-neg ResM (of/getLeft OfA) OfE 
                   -> used-as-neg ResM (of/getRight OfA) OfE 
                   -> used-as-neg ResM OfA OfE.

used-as-neg/sum-unused : used-as-neg ResM OfA OfL
                      -> used-as-neg ResM OfA OfR
                      -> used-as-neg ResM OfA (of/switch _ _ OfL OfR _).

used-as-neg/sum-used : used-as-neg ResM OfA OfI
                    -> used-as-neg ResM OfA (of/switch _ _ _ _ OfI).

used-as-neg/left : used-as-neg ResM OfA OfI
                    -> used-as-neg ResM OfA (of/left _ _ _ OfI).

used-as-neg/right : used-as-neg ResM OfA OfI
      	         -> used-as-neg ResM OfA (of/right _ _ _ OfI).

%worlds (all-b) (used-as-neg _ _ _).

well-moded : of E T -> type.
wm/empty : well-moded of/empty.
wm/obj : well-moded (of/obj OfL OfR) <- well-moded OfR <- well-moded OfL.
wm/getLeft : well-moded (of/getLeft Of) <- well-moded Of.
wm/getRight : well-moded (of/getRight Of) <- well-moded Of.
wm/left : well-moded (of/left _ _ _ Of) <- well-moded Of.
wm/right : well-moded (of/right _ _ _ Of) <- well-moded Of.
wm/switch : well-moded (of/switch _ _ OfL OfR OfI) <- well-moded OfI <- well-moded OfR <- well-moded OfL.
wm/logic-var : well-moded (of/logic-var _).
wm/subsump : well-moded (of/subsump _ _ Of) <- well-moded Of.
wm/@s : well-moded (of/@s OfF OFV) <- well-moded OfV <- well-moded OfF.
wm/lam/+* : well-moded (of/lam F_Of (TmsA : tp-mode-safe A pos) TmsB)
         <- ({x}{a}{sm_a}{s}{of_x} well-moded of_x -> well-moded (F_Of x a sm_a s of_x)).
wm/lam/- : well-moded (of/lam F_Of (TmsA : tp-mode-safe A neg) (TmsB : tp-mode-safe B M) )
      <- ({x}{a}{sm_a}{s}{of_x} used-as-neg M of_x (F_Of x a sm_a s of_x))
      <- ({x}{a}{sm_a}{s}{of_x} well-moded of_x -> well-moded (F_Of x a sm_a s of_x)).

%block wm-assm-b : some {x}{t}{of_x:of x t} block {_:well-moded of_x}.
%block all-b' = (all-b | wm-assm-b).
%worlds (all-b') (well-moded _).