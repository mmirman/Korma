% getting to unsafe-mode in emacs: twelf-server-send-command "set unsafe true"

%%%%%%%%%%%%%%%%%%%%%%%%
% what resolvedness is %
%%%%%%%%%%%%%%%%%%%%%%%%
res : type.
ground : res.
unground : res.
%mode res.
%worlds () (res).

%freeze res.
%name res G g.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% an unground and anything %
% else make an unground    %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%
connect : res -> res -> res -> type.
connect/ul : connect unground ground unground.
connect/ur : connect ground unground unground.
connect/u : connect unground unground unground.
connect/g : connect ground ground ground.

%mode connect +A +B -C.
%worlds () (connect _ _ _).
%freeze connect.
%name connect Con con.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% the types in the language %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
tp : type.
~> : tp -> tp -> tp. %infix right 1 ~>.
unit : tp.
answer : tp.
sum : tp -> tp -> tp.
prod : tp -> tp -> tp.
forall : (tp -> tp) -> tp.
mu : (tp -> tp) -> tp.
%freeze tp.
%name tp T t.

%%%%%%%%%%%%%%%%%%%%%%%
% Predicates on types %
%%%%%%%%%%%%%%%%%%%%%%%
arrowless : tp -> type.
arrowless/unit : arrowless unit.
arrowless/answer : arrowless answer.
arrowless/sum : arrowless (sum A B) <- arrowless A <- arrowless B.
arrowless/prod : arrowless (prod A B) <- arrowless A <- arrowless B.
arrowless/forall : arrowless (forall V) <- {A} arrowless A -> arrowless (V A).
arrowless/mu : arrowless (mu V) <- {A} arrowless A -> arrowless (V A).
%mode arrowless +A.
%block var-arrowless : block {A} {_:arrowless A}.
%worlds (var-arrowless) (arrowless _).
%freeze arrowless.

finite : tp -> type.
finite/arrow : finite (A ~> B)
      <- finite B
      <- finite A.
finite/sum  : finite (sum A B) <- finite A <- finite B.
finite/prod : finite (prod A B) <- finite A <- finite B.

%mode finite +A.
%worlds () (finite _).
%freeze finite.

matchable : tp -> type.
% matchable/finite : matchable A <- finite A.
matchable/arrowless : matchable A <- arrowless A.
% matchable/sum : matchable (sum A B) <- matchable A <- matchable B.
% matchable/prod : matchable (prod A B) <- matchable A <- matchable B.

%mode matchable +A.
%worlds () (matchable _).
%freeze matchable.

%%%%%%%%%%%%%%%%%%%%%%%%%%%
% ascriptions for lambdas %
%%%%%%%%%%%%%%%%%%%%%%%%%%%
ascription : tp -> type.
ascription/matchable : matchable T -> res -> ascription T.
ascription/unmatchable : ascription T.

%mode ascription +T.
%worlds () (ascription _).
%freeze ascription.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% an unground and anything %
% else make an unground    %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%
subsumpG : res -> res -> type.
subsumpG/eq : subsumpG A A.
subsumpG/neq : subsumpG unground ground.

%worlds () (subsumpG _ _).
%mode subsumpG +A +B.
%freeze subsumpG.
%name subsumpG SubG s.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% The term syntax and type rules %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
tm : type.

% in progress element
elem : tp -> type.
elem/resolved : tm -> tm -> elem A.
elem/unresolved : tm -> elem A.

% the definition of a stack
%%%%%%%%%%%%%%%%%%%%%%%%%%%
stack : tp -> type.
nil : stack A.
push : elem A -> stack A -> stack A.

% some the definition of a queue
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
queue : tp -> type.
queue/str : stack A -> stack A -> queue A.

% The definition of a tm
%%%%%%%%%%%%%%%%%%%%%%%%
lam : ascription T -> (tm -> tm) -> tm.
Lam : (tp -> tm) -> tm.
app : tm  -> tm -> tm.
empty : tm.
success : tm.
fail : tm.
obj : tm -> tm -> tm.
getLeft : tm.
getRight : tm.
left : tm -> tm.
right : tm -> tm.
switch : tm -> tm -> tm.
findAll : {A : tp} matchable A -> queue A -> tm -> tm.

roll : tm -> tp -> tm.
unroll : tm -> tp -> tm.
logic-var : tm.
% ascription
# : tm -> tp -> tm. %infix left 5 #.

%freeze queue stack tm.
%name tm E e.

is : tm -> tp -> type. %infix left 5 is.
is/lam : ({x:tm} x is T1 -> (E x) is T2) 
      -> ((lam (_:ascription T1) ([x] E x)) is (T1 ~> T2)).

is/Lam : ({a:tp} (E a) is (T a))
      -> (Lam E) is forall T.

is/roll : E is (T (mu ([a:tp] T a))) ->
          (roll E (mu ([a:tp] T a))) is (mu ([a:tp] T a)).

is/unroll : E is (mu ([a:tp] T a)) ->
          (unroll E (mu ([a:tp] T a))) is (T (mu ([a:tp] T a))).

is/empty : empty is unit.
is/success : success is answer.
is/fail : fail is answer.
is/obj : E is T -> E' is T' -> obj E E' is prod T T'.
is/getLeft : getLeft is forall [a] forall ([b] prod a b ~> a).
is/getRight : getLeft is forall [b] forall ([a] prod a b ~> b).
is/left : E is A -> left E is sum A B.
is/right : E is B -> right E is sum A B.
is/switch : E is (A ~> C) -> E' is (B ~> C) -> switch E E' is (sum A B ~> C).
is/findAll : E is (A ~> answer) -> 
	     findAll A _ _ E is sum A unit.
is/logic-var : logic-var is A.

%%%%%%%%%%%%%%%%%%%%%%%%%
% definition of a maybe %
%%%%%%%%%%%%%%%%%%%%%%%%%
maybe : tp -> type.
just : elem A -> maybe A.
nothing : maybe A.
%freeze maybe.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% some theorems about queues %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

pop : stack A -> stack A -> maybe A -> type.
- : pop nil nil nothing.
- : pop (push E S) S (just E).
%mode pop +A -B -C. %worlds () (pop _ _ _). %freeze pop. %total D (pop D _ _).

reverse : stack A -> stack A -> type.
rev' : stack A -> stack A -> stack A -> type.
- : rev' nil S S.
- : rev' (push E S) S' R
 <- rev' S (push E S') R.
%mode rev' +A +B -C. %worlds () (rev' _ _ _). %total D (rev' D _ _).
- : reverse S S' <- rev' S nil S'.
%mode reverse +S -S'. %worlds () (reverse _ _). %freeze rev' reverse. %total D (reverse D _).

enqueue : elem A -> queue A -> queue A -> type.
- : enqueue A (queue/str Si So) (queue/str (push A Si) So).
%mode enqueue +A +Qi -Qo. %worlds () (enqueue _ _ _). %freeze enqueue. 
%total D (enqueue D _ _). %total D (enqueue _ D _).

dequeue : queue A -> queue A -> maybe A -> type.
- : dequeue (queue/str nil nil) (queue/str nil nil) nothing.
- : dequeue (queue/str S (push A S')) (queue/str S S') (just A).
- : dequeue (queue/str S nil) (queue/str nil S'') Ret
 <- reverse S S'
 <- pop S' S'' Ret.
%mode dequeue +Qi -Qo -E. %worlds () (dequeue _ _ _). %freeze dequeue. %total D (dequeue D _ _).

queue-empty : queue A -> type.
- : queue-empty (queue/str nil nil).
%mode queue-empty +Q. %worlds () (queue-empty _). %freeze queue-empty. 

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% What constitutes a final value %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
value : tm -> type.
value/empty : value empty.
value/success : value success.
value/fail : value fail.
value/lam : value (lam _ _).
value/Lam : value (Lam _).
value/unroll : value A -> value (unroll A _).
value/roll : value A -> value (roll A _).
value/obj : value (obj _ _).
value/left : value (left _).
value/right : value (right _).
value/getLeft : value getLeft.
value/getRight : value getRight.
value/switch : value (switch _ _).
value/findAll : queue-empty V -> value (findAll _ _ V _).
%mode value +V.
%worlds () (value _).
%freeze value.
%name value V v.

resolved : tm -> type.
resolved/empty : resolved empty.
resolved/success : resolved success.
resolved/fail : resolved fail.
resolved/obj : resolved (obj A B) <- resolved A <- resolved B.
resolved/left : resolved (left A) <- resolved A.
resolved/right : resolved (right B) <- resolved B.
%mode resolved +V.
%worlds () (resolved _).
%freeze resolved.
%name resolved V v.

unresolved : tm -> type.
unresolved/obj-left : unresolved (obj A B) <- unresolved A.
unresolved/obj-right : unresolved (obj A B) <- unresolved B.
unresolved/left : unresolved (left A) <- unresolved A.
unresolved/right : unresolved (right B) <- unresolved B.
unresolved/logic-var : unresolved logic-var.
%mode unresolved +V.
%worlds () (unresolved _).
%freeze unresolved.
%name unresolved V v.

% we can approximate resolve-more by always one child
% into exactly two new children (given we only have binary sums/products)
% even if they need to be identical.
resolve        : tp -> tm -> tm -> tm -> type.
resolve/unit   : resolve unit logic-var empty empty.
resolve/answer : resolve answer logic-var success fail.
resolve/prod   : resolve (prod _ _) logic-var (obj logic-var logic-var) (obj logic-var logic-var).
resolve/sum    : resolve (sum _ _) logic-var (left logic-var) (right logic-var).
% todo - finish resolve and operational semantics.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% High level Operational Semantics %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% inherent in the step is the proof of preservation
|-> : tm -> tm -> type. %infix none 10 |->.

step/app-1 : app E1 E2 |-> app E1' E2
	  <- E1 |-> E1'.

step/app-2 : app E1 E2 |-> app E1 E2'
          <- value E1
	  <- E2 |-> E2'.

step/lam : app (lam _ V) E |-> V E
        <- value E.

step/#1 : E |-> E' 
       -> (E # T) |-> (E' # T).
step/#2 : ((Lam E)  # T)  |-> E T.
step/getLeft : app getLeft (obj L _) |-> L.
step/getRight : app getRight (obj _ R) |-> R.

step/switch-left : app (switch LF _) (left L) |-> app LF L.
step/switch-right : app (switch _ RF) (right R) |-> app RF R.

step/findAll-start : findAll S M Q L |-> findAll S M Q' L
    		  <- queue-empty Q
                  <- enqueue (elem/unresolved logic-var) Q Q'.

step/findAll-succ : findAll _ _ Q _ |-> left Solved
  		 <- dequeue Q _ (just (elem/resolved Solved success)).

step/findAll-end-fail : findAll _ _ Q _ |-> right empty
		     <- dequeue Q Q' (just (elem/resolved S fail))
		     <- queue-empty Q'.

step/findAll-fail : findAll T M Q L |-> findAll T M Q' L
		 <- dequeue Q Q' (just (elem/resolved S fail)).

step/findAll-step : findAll T M Q L |-> findAll T M Q'' L
  		 <- dequeue Q Q' (just (elem/resolved S E))
                 <- E |-> E'
                 <- enqueue (elem/resolved S E') Q' Q''.

step/findAll-resolved : findAll T M Q L |-> findAll T M Q'' L
		     <- dequeue Q Q' (just (elem/unresolved S))
		     <- resolved S
		     <- enqueue (elem/resolved S (app L E')) Q' Q''.

step/findAll-unresolved : findAll T M Q L |-> findAll T M Qo L
		       <- dequeue Q Q' (just (elem/unresolved S))
		       <- resolve T S Sa Sb
		       <- enqueue (elem/unresolved Sa) Q' Qinter
		       <- enqueue (elem/unresolved Sb) Qinter Qo.

%freeze |->. %name |-> Step.

not_stuck : tm -> type.
not_stuck/val : value E
             -> not_stuck E.
not_stuck/step : E |-> E'
              -> not_stuck E.

%freeze not_stuck. %name not_stuck NotStuck.
